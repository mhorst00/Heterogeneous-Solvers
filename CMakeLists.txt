cmake_minimum_required(VERSION 3.11)
project(Heterogeneous_SPD_solvers)

set(CMAKE_CXX_STANDARD 17)

option(ENABLE_TESTS "Enable Test" OFF)
option(USE_DOUBLE "Use FP64 precision instead of FP32 for floating point data types" ON)
option(SHARED_MEMORY "Enable shared memory allocation for matrix data" OFF)
set(GPU_VENDOR "NVIDIA" CACHE STRING "GPU vendor: NVIDIA, AMD and INTEL are supported")
set(HWS_SAMPLING_INTERVAL_DEFAULT 10 CACHE STRING "Default sampling interval for the hws library")

# DPC++ only options
option(USE_DPCPP "Use DPC++" OFF)
set(DPCPP_ARCH "" CACHE STRING "Device Architecture for DPC++")

# DPC++ only
if (${USE_DPCPP})
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fsycl")
    if (${GPU_VENDOR} STREQUAL  "NVIDIA")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fsycl-targets=nvidia_gpu_${DPCPP_ARCH},spir64_x86_64")
    endif ()
else ()
    # find AdaptiveCpp SYCL implementation
    find_package(AdaptiveCpp CONFIG REQUIRED)
endif ()


# AdaptiveCpp only
if (NOT ${USE_DPCPP})
    # find OpenMP
    find_package(OpenMP REQUIRED)
endif ()

# include FetchContent
include(FetchContent)

# export compile commands for clangd usage
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# define all program source files for the executable
set(EXECUTABLE_FILES src/main.cpp)

# define all library files that will be linked to the executable
set(LIBRARY_FILES
        src/utility/Configuration.cpp
        src/utility/Configuration.hpp
        src/utility/MatrixParser.cpp
        src/utility/MatrixParser.hpp
        src/utility/MatrixParserMixed.cpp
        src/utility/MatrixParserMixed.hpp
        src/utility/UtilityFunctions.cpp
        src/utility/UtilityFunctions.hpp
        src/utility/MatrixGenerator.cpp
        src/utility/MatrixGenerator.hpp
        src/utility/MatrixGeneratorMixed.cpp
        src/utility/MatrixGeneratorMixed.hpp
        src/utility/RankFinder.hpp
        src/utility/RankFinder.cpp
        src/solver/cg/CG.cpp
        src/solver/cg/CG.hpp
        src/solver/cg/CGMixed.cpp
        src/solver/cg/CGMixed.hpp
        src/solver/cholesky/Cholesky.cpp
        src/solver/cholesky/Cholesky.hpp
        src/solver/cholesky/CholeskyMixed.cpp
        src/solver/cholesky/CholeskyMixed.hpp
        src/solver/cholesky/TriangularSystemSolver.cpp
        src/solver/cholesky/TriangularSystemSolver.hpp
        src/solver/cholesky/TriangularSystemSolverMixed.cpp
        src/solver/cholesky/TriangularSystemSolverMixed.hpp
        src/gaussianProcess/GaussianProcess.cpp
        src/gaussianProcess/GaussianProcess.hpp
        src/dataStructures/SymmetricMatrix.cpp
        src/dataStructures/SymmetricMatrix.hpp
        src/dataStructures/SymmetricMatrixMixed.cpp
        src/dataStructures/SymmetricMatrixMixed.hpp
        src/dataStructures/RightHandSide.cpp
        src/dataStructures/RightHandSide.hpp
        src/kernel/VectorOperations.cpp
        src/kernel/VectorOperations.hpp
        src/kernel/MatrixVectorOperations.cpp
        src/kernel/MatrixVectorOperations.hpp
        src/kernel/MatrixVectorOperationsMixed.cpp
        src/kernel/MatrixVectorOperationsMixed.hpp
        src/kernel/MatrixOperations.cpp
        src/kernel/MatrixOperations.hpp
        src/kernel/MatrixOperationsMixed.cpp
        src/kernel/MatrixOperationsMixed.hpp
        src/kernel/MatrixMatrixOperations.cpp
        src/kernel/MatrixMatrixOperations.hpp
        src/kernel/MatrixMatrixOperationsMixed.cpp
        src/kernel/MatrixMatrixOperationsMixed.hpp
        src/metricsTracker/MetricsTracker.cpp
        src/metricsTracker/MetricsTracker.hpp
        src/loadBalancer/LoadBalancer.cpp
        src/loadBalancer/LoadBalancer.hpp
        src/loadBalancer/StaticLoadBalancer.cpp
        src/loadBalancer/StaticLoadBalancer.hpp
        src/loadBalancer/RuntimeLoadBalancer.cpp
        src/loadBalancer/RuntimeLoadBalancer.hpp
        src/loadBalancer/PowerLoadBalancer.cpp
        src/loadBalancer/PowerLoadBalancer.hpp)

add_library(heterogeneous_solvers_lib ${LIBRARY_FILES})

include_directories(src)
include_directories(src/dataStructures)
include_directories(src/utility)
include_directories(src/solver)
include_directories(src/solver/cg)
include_directories(src/solver/cholesky)
include_directories(src/kernel)
include_directories(src/loadBalancer)
include_directories(src/metricsTracker)

# create and executable, add SYCL to executable and link OpenMP and custom library files
add_executable(heterogeneous_solvers ${EXECUTABLE_FILES})
target_link_libraries(heterogeneous_solvers PUBLIC heterogeneous_solvers_lib)

# AdaptiveCpp only
if (NOT ${USE_DPCPP})
    add_sycl_to_target(TARGET heterogeneous_solvers SOURCES ${EXECUTABLE_FILES})
    target_link_options(heterogeneous_solvers PUBLIC -fopenmp)
endif ()

# enable FP64 double precision
if (USE_DOUBLE)
    target_compile_definitions(heterogeneous_solvers_lib PUBLIC USE_DOUBLE=1)
endif ()

if (${USE_DPCPP})
    target_compile_definitions(heterogeneous_solvers_lib PUBLIC USE_DPCPP=1)
endif ()

# enable shared memory allocation for matrix data
if (SHARED_MEMORY)
        target_compile_definitions(heterogeneous_solvers_lib PUBLIC SHARED_MEMORY=1)
endif ()

target_compile_definitions(heterogeneous_solvers_lib PUBLIC ${GPU_VENDOR})
target_compile_definitions(heterogeneous_solvers_lib PUBLIC HWS_SAMPLING_INTERVAL_DEFAULT=${HWS_SAMPLING_INTERVAL_DEFAULT})

message("Building the hardware sampling library")
# build and link the hws library
set(HWS_ENABLE_PYTHON_BINDINGS OFF CACHE INTERNAL "" FORCE)
set(HWS_SAMPLING_INTERVAL ${HWS_SAMPLING_INTERVAL_DEFAULT} CACHE INTERNAL "" FORCE)
set(CMAKE_SUPPRESS_DEVELOPER_WARNINGS TRUE CACHE BOOL "" FORCE)


FetchContent_Declare(
        hws
        GIT_REPOSITORY https://github.com/SC-SGS/hardware_sampling
        GIT_TAG fbd284d7d18081c4b44fbff3b402afbe8a81610a
)
FetchContent_MakeAvailable(hws)
add_dependencies(heterogeneous_solvers hws)
target_include_directories(heterogeneous_solvers PUBLIC ${hws_SOURCE_DIR}/include)
target_link_libraries(heterogeneous_solvers PUBLIC hws::hws)
target_link_libraries(heterogeneous_solvers_lib PUBLIC hws::hws)

# include the cxxopts library for cli arguments
FetchContent_Declare(
        cxxopts
        GIT_REPOSITORY https://github.com/jarro2783/cxxopts
        GIT_TAG v3.2.0
)
FetchContent_MakeAvailable(cxxopts)
add_dependencies(heterogeneous_solvers cxxopts)
target_include_directories(heterogeneous_solvers PUBLIC ${cxxopts_SOURCE_DIR}/include)

# Tests
if (${ENABLE_TESTS})
    set(TEST_SOURCE_FILES
            tests/testSymmetricMatrix.cpp
            tests/testSymmetricMatrixMixed.cpp
            tests/testMatrixVectorBlock.cpp
            tests/testVectorOperations.cpp
            tests/testMatrixOperationsMixed.cpp
            tests/testMatrixOperations.cpp
            tests/testMatrixMatrixOperations.cpp)

    FetchContent_Declare(
            googletest
            GIT_REPOSITORY https://github.com/google/googletest.git
            GIT_TAG v1.16.0
    )
    set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
    FetchContent_MakeAvailable(googletest)

    add_executable(heterogeneous_solvers_test ${TEST_SOURCE_FILES})
    enable_testing()
    include(GoogleTest)
    gtest_discover_tests(heterogeneous_solvers_test)
    target_link_libraries(heterogeneous_solvers_test GTest::gtest_main heterogeneous_solvers_lib)
    message("Tests are enabled")
endif ()
